#################################
rule annovar:
    input:
        "table/{sample}_{tumor_normal}.csv"
    output:
        "table/{sample}_{tumor_normal}.anno.csv"
    log:
        "logs/annovar/{sample}_{tumor_normal}.log"
    threads:
        config["annovar"]["threads"]
    params:
        anno = get_anno_params,
    conda:
        f"../{config['envs']}/eb-env.yml"
    script:
        "../scripts/anno.py"


rule fisher_strand:
    input:
        "table/{sample}_{tumor_normal}.csv"
    output:
        "table/{sample}_{tumor_normal}.fisher.csv"
    log:
        "logs/fisher/{sample}_{tumor_normal}.log"
    threads:
        config["FisherStrand"]["threads"]
    conda:
        f"../{config['envs']}/eb-env.yml"
    script:
        "../scripts/fisher_strand.py"


rule merge_annos:
    input:
        annovar = "table/{sample}_{tumor_normal}.anno.csv",
        fisher = "table/{sample}_{tumor_normal}.fisher.csv",
        eb_score = "table/{sample}_{tumor_normal}.EB.csv"
    output:
        "table/{sample}_{tumor_normal}.raw.csv"
    threads:
        1
    params:
        info_folder=os.path.join(snakedir, "info")
    script:
        "../scripts/merge_annos.py"


rule edit_table:
    input:
        "table/{sample}_{tumor_normal}.raw.csv"
    output:
        "filter/{sample}_{tumor_normal}.csv"
    threads:
        1
    params:
        snake_folder=snakedir
    script:
        "../scripts/edit_table.py"
