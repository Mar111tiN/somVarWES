rule TN2CNV:
    input:
        pileup = "pileup/{sample}_{tumor}-{normal}.{chrom}.gz"
    output: 
        cov = "cnv/{sample}_{tumor}-{normal}.{chrom}.cov.gz",
        snp = "cnv/{sample}_{tumor}-{normal}.{chrom}.snp"
    params:
        genmap_split_path = full_path('genmap_split'),
        gc_split_path = full_path('gcratio_split'),
        bedfile = full_path('bed_file')
    threads:
        config['CNV']['coverage']['threads']
    conda:
        f"../{config['envs']}/sampy-env.yml"
    script:
        "../scripts/CNV_snake.py"


rule combineCNV:
    input:
        cov = expand("cnv/{{sample}}_{{tumor}}-{{normal}}.{chrom}.cov.gz", chrom=chrom_list),
        snp = expand("cnv/{{sample}}_{{tumor}}-{{normal}}.{chrom}.snp", chrom=chrom_list)
    output:
        cov = "CNV/{sample}_{tumor}-{normal}.cov.gz",
        snp = "CNV/{sample}_{tumor}-{normal}.snp",
        plot = "CNV/{sample}/plots/{sample}_{tumor}-{normal}_{tumor}.snp.raw.jpg",
        CNV = "CNV/{sample}/data/{sample}_{tumor}-{normal}.cnv.snp.gz",
        fullCNV = "CNV/{sample}/data/{sample}_{tumor}-{normal}.cnv.gz"
    params:
        PON_path = static_path(config['EBscore']['pon_path']),
    threads:
        config['CNV']['combine']['threads']
    conda:
        f"../{config['envs']}/plot-env.yml"
    script:
        "../scripts/CNV_combine.py"


rule prepareASCAT:
    input:
        CNV = "CNV/{sample}/data/{sample}_{tumor}-{normal}.cnv.snp.gz"
    output:
        bafT = "CNV/{sample}/pre/{sample}_{tumor}-{normal}_{tumor}_baf_tumor.tsv",
        bafN = "CNV/{sample}/pre/{sample}_{tumor}-{normal}_{normal}_baf_tumor.tsv",
        bafGerm = "CNV/{sample}/pre/{sample}_{tumor}-{normal}_baf_normal.tsv",
        logrT = "CNV/{sample}/pre/{sample}_{tumor}-{normal}_{tumor}_logr_tumor.tsv",
        logrN = "CNV/{sample}/pre/{sample}_{tumor}-{normal}_{normal}_logr_tumor.tsv",
        logrGerm = "CNV/{sample}/pre/{sample}_{tumor}-{normal}_logr_normal.tsv"
    threads:
        1
    script:
        "../scripts/preASCAT.py"


rule runASCAT:
    input:
        bafT = "CNV/{sample}/pre/{sample}_{tumor}-{normal}_{type}_baf_tumor.tsv",
        bafGerm = "CNV/{sample}/pre/{sample}_{tumor}-{normal}_baf_normal.tsv",
        logrT = "CNV/{sample}/pre/{sample}_{tumor}-{normal}_{type}_logr_tumor.tsv",
        logrGerm = "CNV/{sample}/pre/{sample}_{tumor}-{normal}_logr_normal.tsv"
    output: 
        cnv = "CNV/{sample}/{sample}_{tumor}-{normal}_{type}.ASCATprofile.png"
    conda:
        "../env/R-env.yaml"
    threads:
        1
    script:
        "../scripts/ascat.R" 


rule SNPlot:
    input:
        CNV = "CNV/{sample}/{sample}_{tumor}-{normal}.cnv.snp.gz"
    output:
        plot="CNV/{sample}/{sample}_{tumor}-{normal}.SNPmatch.jpg",
    threads:
        1
    conda:
        f"../{config['envs']}/plot-env.yml"
    script:
        "../scripts/plot_SNP.py"
