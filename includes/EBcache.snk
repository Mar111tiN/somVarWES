rule EBcacheMatrix:
    input:
        config['EBscore']['pon_list']
    output:
        "matrix/{chrom}.pon.gz"
    threads:
        config['EBscore']['threads']['EBcacheMatrix']
    conda:
        f"../{config['envs']}/eb-env.yml"
    params:
        pon_path = static_path(config['EBscore']['pon_path']),
        pon_list = config['EBscore']['pon_list'],
        genome_split = full_path('genome_split'),
        bedfile = full_path('bed_file')
    script:
        "../scripts/EBcache_matrix.py"


rule EBcacheZeros:
    output:
        touch("zero/done")
    threads:
        4
    conda:
        f"../{config['envs']}/eb-env.yml"
    params:
        pon_path = static_path(config['EBscore']['pon_path']),
        pon_list = config['EBscore']['pon_list'],
        reflat = config['EBscore']['reflat']
    script:
        "../scripts/EBcache_zeros.py"

rule EBcacheAB:
    input:
        zero="zero/done",
        matrix="matrix/{chrom}.pon.gz"
    output:
        "splitAB/{chrom}.{split}.AB.gz"
    threads:
        config['EBscore']['threads']['EBcacheAB']
    conda:
        f"../{config['envs']}/eb-env.yml"
    params:
        pon_path = static_path(config['EBscore']['pon_path'])
    script:
        "../scripts/EBcache_AB.py"


rule combineEBcacheAB:
    input:
        expand("splitAB/{{chrom}}.{split}.AB.gz", split=list(range(config['EBscore']['ABcache_split'])))
    output:
        "AB/{chrom}.AB.gz"
    threads:
        2
    run:
        dfs = []
        for file in input:
            df = pd.read_csv(file, sep="\t", compression="gzip")
            dfs.append(file)
        AB_df = pd.concat(dfs).sort_values("Start")
        AB_df.to_csv(output, sep="\t", index=False, compression="gzip")


rule EBcacheDone:
    input:
        expand("AB/{chrom}.AB.gz", chrom=chrom_list)
    output:
        touch("PONcache.done")
    params:
        pon_path = static_path(config['EBscore']['pon_path']),
        pon_list = config['EBscore']['pon_list'],
        reflat = False
    threads:
        6
    conda:
        f"../{config['envs']}/eb-env.yml"
    script:
        "../scripts/EBcache_zeros.py"
